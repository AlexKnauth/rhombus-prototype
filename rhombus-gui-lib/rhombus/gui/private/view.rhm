#lang rhombus/static/and_meta
import:
  lib("racket/gui/easy.rkt"):
    expose:
      #{if-view}
      #{cond-view}
      #{observable-view}
  lib("racket/base.rkt").else
  "obs.rhm"
  "type.rhm"

export:
  View
  WindowView

module private:
  export:
    to_view

interface View:
  property handle

  export:
    if
    cond
    type.View.SizeInt
    type.View.Size
    type.View.SizeLike
    type.View.PositionInt
    type.View.Position
    type.View.PositionLike
    type.View.SpacingInt
    type.View.Margin
    type.View.MarginLike
    type.View.HorizAlignment
    type.View.VertAlignment
    type.View.Alignment
    type.View.AlignmentLike
    type.View.Stretch
    type.View.StretchLike
    type.View.LabelString

  expr.macro 'if $test | $then | $else':
    'AView($(expr_meta.pack_s_exp(['#{if-view}',
                                   expr_meta.pack_expr('obs.unwrap(block: $test)'),
                                   expr_meta.pack_expr('unwrap_view(block: $then)'),
                                   expr_meta.pack_expr('unwrap_view(block: $else)')])))'

  expr.macro
  | 'cond
     | $ques ...: $ans
     | ...
     | ~else: $else_ans':
      'AView($(expr_meta.pack_s_exp(['#{cond-view}',
                                     [expr_meta.pack_expr('obs.unwrap($ques ...)'),
                                      expr_meta.pack_expr('unwrap_view(block: $ans)')],
                                     ...,
                                     ['else',
                                      expr_meta.pack_expr('unwrap_view(block: $else_ans)')]])))'
  | 'cond
     | $ques ...: $ans
     | ...
     | ~else $else':
      'cond
       | $ques ...: $ans
       | ...
       | ~else: $else'

interface WindowView:
  extends View

fun unwrap_view(v :: View): v.handle

class AView(private _handle):
  implements View
  override property handle: _handle

  constructor (hand):
    super(hand)

  private implements Printable
  private override describe(mode, recur):
    PrintDesc.list("View(", [], ")")

fun
| to_view(v :: View) :: View: v
| to_view(o :: obs.Obs) :: View: AView(#{observable-view}(o.map(fun (v :: View): v.handle).handle))
