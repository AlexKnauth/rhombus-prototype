#lang rhombus
import:
  rhombus/macro: no_prefix
  "typeset-rhombus.rkt": no_prefix
  "typeset_meta.rhm":
    for_meta
  scribble/base:
    prefix scribble
    expose: elem

export:
  rhombus
  rhombusblock

for_meta:
  def | map(proc, []): []
      | map(proc, [a, as, ...]): cons(proc(a), map(proc, as))

  def | andmap(proc, []): #true
      | andmap(proc, [a, as, ...]): proc(a) && andmap(proc, as)

  def | map2(proc, [], []): []
      | map2(proc, [a, as, ...], [b, bs, ...]): cons(proc(a, b), map2(proc, as, bs))

  def head_context(stxs):
    match stxs
    | '($head $_ ......):
        relocate_syntax('#false, head)

  def head_escape_context(stxs):
    match stxs
    | '($head $esc $_ ......):
        relocate_span_syntax('#false, [head, esc])

  def literal(stxs):
    '(literal_syntax $stxs)

  def is_literal(stx):
    match stx
    | '(literal_syntax $_): #true
    | ~else: #false

  def build_relocate_group(ts, g):
    if is_literal(ts)
    | ts
    | '(as_seq(relocate_group($ts, literal_syntax_term($(relocate_syntax('#false, g))))))

  def nested(gs, builder, stxs):
    val tss: map(fun(g): escape('($g)), gs)
    if andmap(is_literal, tss)
    | literal(stxs)
    | val new_gs: map2(build_relocate_group, tss, gs)
      '($builder([$new_gs, ...], literal_syntax_term($(head_context(stxs)))))

  def escape(stxs):
    match stxs
    | '($$($expr ...) $tail ......):
        '(sequence_cons_syntax(elem($expr ...), $(escape(tail)),
                               literal_syntax_term($(head_escape_context(stxs)))))
    | '($head $next $tail ......):
        val new_head: escape('($head))
        val new_tail: escape('($next $tail ......))
        if is_literal(new_head)  && is_literal(new_tail)
        | literal(stxs)
        | '(sequence_append_syntax($(escape('($head))), $new_tail,
                                   literal_syntax_term($(head_context(stxs)))))
    | '(($gs, ...)):
        nested(gs, 'parens_syntax, stxs)
    | '([$gs, ...]):
        nested(gs, 'brackets_syntax, stxs)
    | '({$gs, ...}):
        nested(gs, 'braces_syntax, stxs)
    | '(: $gs; ...):
        nested(gs, 'block_syntax, stxs)
    | '($(id $: Id)):
        val mv: syntax_meta_value(typeset_meta.in_space(id), #false)
        match mv
        | typeset_meta.Transformer(proc):
            '(relocate_expansion($(proc(id)),
                                 literal_syntax_term($(head_context(stxs)))))
        | ~else: literal(stxs)
    | ~else: literal(stxs)

fun as_seq(v): '($v)

fun sequence_cons_syntax(a, d, context):
  let a_r: relocate_syntax(to_syntax(a), context);
  match d
  | '($e ......): '($a_r $e ......)

fun sequence_append_syntax(d1, d2, context):
  match d1
  | '($e1 ...):
      match d2
      | '($e2 ......):
          relocate_syntax('($e1 ... $e2 ......), context)
      
fun parens_syntax(ts, context):
  match ts
  | ['($t), ...]: as_seq(relocate_syntax('($t, ...), context))

fun brackets_syntax(ts, context):
  match ts
  | ['($t), ...]: as_seq(relocate_syntax('[$t, ...], context))

fun braces_syntax(ts, context):
  match ts
  | ['($t), ...]: as_seq(relocate_syntax('{$t, ...}, context))

fun block_syntax(ts, context):
  match ts
  | ['($t), ...]: as_seq(relocate_syntax(syntax_term(: $t; ...), context))

fun relocate_group(ts, context):
  match ts
  | '($g): relocate_syntax(g, context)

fun relocate_expansion(e, context):
  as_seq(relocate_syntax(to_syntax(e), context))

// ----------------------------------------

expr.macro '(rhombus ($forms ...) $main_tail ......):
  values('(#{typeset-rhombus}($(escape('($forms ...))))),
         main_tail)

expr.macro '(rhombusblock $tail ...):
  ~op_stx: me
  match '($tail ...)
  | '(: $content ...; ...):
      values('(#{typeset-rhombusblock}($(escape('($tail ...))))), '())
  | ~else: raise_syntax_error("expected a block", '($me $tail ...))
