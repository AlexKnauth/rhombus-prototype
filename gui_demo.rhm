#lang rhombus/static
import:
  rhombus/draw
  rhombus/gui

fun draw_face(dc :: draw.DC, config :: Map):
  def [w, h] = dc.size
  def s = math.max(10, math.min(w, h) - 20)
  def x = (w-s)/2
  def y = (h-s)/2
  def π = math.pi

  dc.push()

  dc.pen := draw.Pen.none
  dc.brush := draw.Brush(~color: "orange")
  dc.ellipse(x, y, s, s)

  dc.pen := draw.Pen(~color: "Black")
  dc.brush := draw.Brush.none
  if config["mood"] == "Happy"
  | dc.arc(x + 0.2*s, y + 0.2*s, 0.6*s, 0.6*s, π * -3/4, π * -1/4)
  | dc.arc(x + 0.2*s, y + 0.7*s, 0.6*s, 0.6*s, π * 1/4, π * 3/4)

  when config["eyes"]
  | dc.pen := draw.Pen.none
    dc.brush := draw.Brush(~color: "black")
    dc.ellipse(x+0.3*s, y+0.3*s, 0.1*s, 0.1*s)
    dc.ellipse(x+0.6*s, y+0.3*s, 0.1*s, 0.1*s)

  when config["moustache"]
  | let p = draw.Path()
    p.move_to(0, 0)
    p.curve_to(20, -10,  80, -10,  100, 0)
    p.curve_to(120, 10,  180, 0,  200, -20)
    p.curve_to(180, 20,  120, 40, 90, 30)
    p.curve_to(60, 20, 20, 20, 0, 0)
    p.close()
    p.scale(s/500, s/500)
    dc.pen := draw.Pen.none
    dc.brush := draw.Brush(~color: "brown")
    dc.path(p, ~dx: x+0.5*s, ~dy: y+0.6*s)
    p.scale(-1, 1)
    dc.path(p, ~dx: x+0.5*s, ~dy: y+0.6*s)

  dc.pop()

def tabs:
  def at_tab = gui.Obs("Happy")
  def eyes = gui.Checkbox("Eyes", ~is_checked: #true)
  def stache = gui.Checkbox("Moustache", ~is_checked: #false)

  def canvas = gui.VPanel(gui.Canvas(gui.Obs.combine({ "mood": at_tab,
                                                       "eyes": eyes.at_is_checked,
                                                       "moustache": stache.at_is_checked } ),
                                     draw_face),
                          gui.HPanel(eyes, stache,
                                     ~stretch: [#false, #false]))

  def happy_button = gui.Button("Be Happy",
                                ~action: fun ():
                                           at_tab.update(fun (v): "Happy"),
                                ~stretch: [#false, #false])

  gui.TabsPanel(["Happy", "Sad"],
                ~selection: at_tab,
                at_tab.map(fun (v):
                             match v
                             | "Happy": canvas
                             | ~else gui.HPanel(canvas, happy_button)))

gui.render(gui.Window(tabs,
                      ~size: [800, 600]))
